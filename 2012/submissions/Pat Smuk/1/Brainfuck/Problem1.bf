RESET

[
	Problem 1 implemented in Brainfuck.
	By Pat Smuk.
	Giving new meaning to "manual memory management".
	(**In Russian Accent**) KEYWORDS IS FOR PUSSIES!
	Tested with the http://repl.it Brainfuck interpreter.

	Notes: 
		Text must be entered one letter at a time on repl.it including spaces.
		
		The search word cannot be longer than 10 characters.
		
		All characters in source code besides plus, minus, dot, comma, square
		brackets, and triangle brackets are comments and therefore not interpreted.
		
		If the word doesn't exist in the text, the program will probably
		loop forever.
]

print "Enter the word to search for:\n"
++++++++++
[
    >+++++++>+++++++++++>++++++++++++>++++++++++>+++++++++++>+++>++++++++++++
	>++++++++++>++++++++++>+++>++++++++++++>+++++++++++>+++++++++++>++++++++++
	>+++>++++++++++++>+++++++++++>+++>++++++++++++>++++++++++>++++++++++
	>+++++++++++>++++++++++>++++++++++>+++>++++++++++>+++++++++++>+++++++++++
	>++++++
	><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-
]
>->>---->+>++++>++>---->++++>+>++>->+>++++>>++>---->+>++>----->+>--->++++>-
>++++>++>++>+>++++>-->++++++++++>
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.

leave enough room for the buffers
>>>>>>>>>>>>>>>>>>>>>>>>>

get the word one character at a time
,----------[>,----------]<[<]>[++++++++++>]

copy into two 20 character buffers
<[<]>
[
    [
        <<<<<<<<<<<<+
        <<<<<<<<<<<+
        >>>>>>>>>>>>>>>>>>>>>>>-
    ]>
]
<<<<<<<<<<<<<<<<<<<<<<<<[<]

go to the start of memory while erasing the previous message
<[[-]<]

print "Enter the text to search:    \n"
++++++++++
[
	>+++++++>+++++++++++>++++++++++++>++++++++++>+++++++++++>+++>++++++++++++
	>++++++++++>++++++++++>+++>++++++++++++>++++++++++>++++++++++++
	>++++++++++++>+++>++++++++++++>+++++++++++>+++>++++++++++++>++++++++++
	>++++++++++>+++++++++++>++++++++++>++++++++++>++++++>+++>+++>+++>+++
	><<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-
]
>->>---->+>++++>++>---->++++>+>++>---->+>>---->++>---->+>++>----->+>--->++++>-
>++++>-->++>++>++>++>++++++++++>
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.

skip past the previously entered word and text buffer
>>>>> >>>>> >>>>> >>>>> >>>>> >>>>> >>>>>

get the text one character at a time
>,----------[>,----------]<[<]>[++++++++++>]

copy the first ten characters to the text buffer
<[<]>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>

set the index cell to 1
<<<<< <<<<<[<]<<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<< <<<< <<<<[-]+>>>>> >>>>> >>>>

for each character in the word
[
	perform the brainfuck equivalent of comparison
	[
		->>>>> >>>>> >>>>> >>>>> >>
		-<<<<< <<<<< <<<<< <<<<< <<
	]
	>>>>> >>>>> >>>>> >>>>> >>
	if the comparison failed
	[
		mark each cell in the buffer with 1
		<<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<<< [>]> +>+>+>+>+>+>+>+>+>+ <<<<< <<<<
		for each character in the word that's been checked
		[
			remove mark
			-
			if zero
			<<<<<<<<<<<<[-]>[-]
			>>>>>>>>>>>[<<<<<<<<<<<<+>+>>>>>>>>>>>-]
			<<<<<<<<<<<<[>>>>>>>>>>>>+<<<<<<<<<<<<-]+
			>[<->[-]]<
			[
				clear a temp byte and go to the character in the second buffer
				<[-]>>>>>>>>>>>>>>>>>>>>>>>>
				copy it to a temp buffer
				[-<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>]
				<<<<<<<<<<<<<<<<<<<<<<<<
				[->>>>>>>>>>>>>+>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<]
				+>>+<-
			]
			+>>>> >>>> >>>> >
		]
		go back to the first byte of the text
		>>>>> >>>>> >>>>> >>>>> >>>>
		remove the first byte and shift the rest one byte left
		[-]>[[-<+>]>]
		go to and erase the text buffer
		<<[<]<<<<<<<<<<<<+++++++++++++
		[
			->++++++++++>++++++++++>++++++++++>++++++++++>++++++++++>++++++++++
			>++++++++++>++++++++++>++++++++++>++++++++++<<<<<<<<<<
		]
		>[[-]>]>>
		copy the first ten characters to the text buffer
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		[-<<<< <<<< <<<+>>>> >>>> >>>]<<<< <<<< <<<
		[-<+>>>> >>>> >>>>+<<<< <<<< <<<]>>>> >>>> >>>>
		increase the index counter
		<<<<< <<<<<[<]
		<<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<< <<<< <<<<+
		>>>>> >>>>> >>>>> >>>>> >>>>> >>>>> >>>>>
	]
	seek back to the first character of the word
	<<<<< <<<<< <<<<< <<<<< <
]

catch the end of the ones tail
<<<<< <<<<< <<
copy the index byte to a safe place
[<]>>>>> >>>>> >>>>> >>>
>>>>> >>>>> >>>>[-]<<<< <<<<< <<<<<
[->>>>> >>>>> >>>>+<<<<< <<<<< <<<<]
clear the temporary space
+[>]<[[-]<]

print "The index of the word is: "
++++++++++
[
	>++++++++>++++++++++>++++++++++>+++>+++++++++++>+++++++++++>++++++++++
	>++++++++++>++++++++++++>+++>+++++++++++>++++++++++>+++>++++++++++++
	>++++++++++>++++++++++>+++>++++++++++++>+++++++++++>+++++++++++>++++++++++
	>+++>+++++++++++>++++++++++++>++++++>+++><<<<<<<<<<<<<<<<<<<<<<<<<<<-
]
>++++>++++>+>++>----->>>+>>++>+>++>++>---->++++>+>++>->+>++++>>++>----->-----
>-->++>
<<<<<<<<<<<<<<<<<<<<<<<<<<<
>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.

go to the index byte and adjust
>>>>>>-

finally print the index
>>++++++++++<<
[
    ->+>-[>+>>]>[+[-<+>]>+>>]<<<<<<
]
>>[-]>>>++++++++++<
[
    ->-[>+>>]>[+[-<+>]>+>>]<<<<<
]
>[-]>>
[
	>++++++[-<++++++++>]<.<<+>+>[-]
]
<
[
	<[->-<]++++++[->++++++++<]>.[-]
]
<<++++++[-<++++++++>]<.[-]<<[-<+>]<

perform cleanup
>>>>> >>>>> >
clean search word buffer
[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>>
clean text buffer
[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>>>
clean text
[>]<[[-]<]
clean index cell
<<<<< <<<<< <<<<< <<<<< <<<<< <<<<< <<<[-]
clean temp data
<<<<<<[[-]<]

Goodnight everybody! *bows*